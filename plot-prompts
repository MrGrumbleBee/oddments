#!/bin/zsh
do_part () { #{{{
    typeset TxtPart HiColor=${2:='%B%F{m}'} BaseColor='%B%F{black}'
    setopt RE_MATCH_PCRE
    TxtPart=$(sort -R ${1} | head -1)
    if [[ ${TxtPart} =~ '^([Tt]he|[Aa]n*) ' ]] {
        TxtPart=${TxtPart/${~match} /${BaseColor}${match} ${HiColor}}
    }
    if [[ ${TxtPart} =~ '\(' ]] {
        TxtPart=${TxtPart:s/(/${BaseColor}(}
    }
    print "${HiColor}${TxtPart}${BaseColor}"
} #}}}
do_prompts () { #{{{
    typeset Sub Obj Mood
    for x ({1..${1}}) {
        Sub=$(do_part ${datdir}/subjects)
        Obj=$(do_part ${datdir}/objects)
        Mood=$(do_part ${datdir}/moods)
        print -Pn "${Sub}, "
        print -Pn "${Obj}, "
        print -P "and ${Mood}."
    }
} #}}}
typeset num=1 datdir="${0:h}/plot-prompts.dat"
[[ "$1" == [0-9]* ]] && num=$1
do_prompts ${num}
