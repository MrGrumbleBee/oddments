#!/bin/zsh
do_part () { #{{{
    typeset TxtPart HiColor=${2:='%B%F{blue}'} BaseColor='%B%F{black}'
    setopt RE_MATCH_PCRE
    TxtPart=$(sort -R ${1} | head -1)
    if [[ ${TxtPart} =~ '^([Tt]he|[Aa]n*) ' ]] {
        TxtPart=${TxtPart/${~match} /${BaseColor}${match} ${HiColor}}
    }
    if [[ ${TxtPart} =~ '\(' ]] {
        TxtPart=${TxtPart:s/(/${BaseColor}(}
    }
    print "${HiColor}${TxtPart}${BaseColor}"
} #}}}
do_prompts () { #{{{
    typeset Protagonist Antagonist Object Theme
    for x ({1..${1}}) {
        Protagonist=$(do_part ${datdir}/subjects)
        Antagonist=$(do_part ${datdir}/subjects)
        Object=$(do_part ${datdir}/objects)
        Theme=$(do_part ${datdir}/themes)
        print -Pn "${Protagonist}, "
        print -Pn "${Antagonist}, "
        print -Pn "${Object}, "
        print -P "and ${Theme}."
    }
} #}}}
typeset num=1 datdir="${0:h}/plot-prompts.dat"
[[ "$1" == [0-9]* ]] && num=$1
do_prompts ${num}
