#!/bin/zsh
typeset HiColor=${2:='%B%F{blue}'} BaseColor='%B%F{black}'
do_part () { #{{{
    typeset TxtPart
    setopt RE_MATCH_PCRE
    TxtPart=$(grep -v '^#' ${1} | sort -R | head -1)
    if [[ ${TxtPart} =~ '^([Tt]he|[Aa]n*) ' ]] {
        TxtPart=${TxtPart/${~match} /${BaseColor}${match} ${HiColor}}
    }
    if [[ ${TxtPart} =~ '\(' ]] {
        TxtPart=${TxtPart:s/(/${BaseColor}(}
    }
    print "${HiColor}${TxtPart}${BaseColor}"
} #}}}
do_prompts () { #{{{
    typeset Protagonist Antagonist Object Theme Need
    for x ({1..${1}}) {
        Protagonist=$(do_part ${datdir}/subjects)
        Antagonist=$(do_part ${datdir}/subjects)
        Object=$(do_part ${datdir}/objects)
        Theme=$(do_part ${datdir}/themes)
        Need=$(do_part ${datdir}/needs)
        print -Pn "${BaseColor}A tale of ${Need}: "
        print -Pn "${Protagonist}, "
        print -Pn "${Antagonist}, "
        print -Pn "${Object}, "
        print -P "and ${Theme}."
    }
} #}}}
typeset num=1 datdir="${0:h}/plot-prompts.dat"
[[ "$1" == [0-9]* ]] && num=$1
do_prompts ${num}
